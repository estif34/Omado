import QtQuick
import Quickshell
import Quickshell.Hyprland
import Quickshell.Io
import Quickshell.Wayland
import qs.Commons
import qs.Ui
import "Model.js" as Model

Panel {
    id: root
    moduleName: "maduki-tech.omado"

    property var anchorItem: null
    property var hostWidget: null
    readonly property var barIdentity: hostWidget || root
    readonly property var hostWindow: hostWidget && hostWidget.QsWindow
        ? hostWidget.QsWindow.window : null
    readonly property var hostScreen: hostWindow ? hostWindow.screen : null
    readonly property string label: ""
    property int remaining: 0

    // Index of the row being edited (-1 = none), plus the TextField holding
    // it so switching to another row can flush the pending text first.
    property int editingIndex: -1
    property var activeEditor: null
    // The selected row is independent of keyboard focus: the text fields may
    // own focus while this index keeps the list's visual cursor in sync.
    property int selectedIndex: -1
    property bool quickAddOpen: false

    readonly property string stateDir: Quickshell.env("HOME") + "/.local/state/omarchy/settings"
    readonly property string todoPath: stateDir + "/maduki-tech.todo.json"

    ListModel {
        id: todoModel
    }

    function openFromHotkey() {
        root.controller.show();
        Qt.callLater(function () {
            if (root.opened)
                root.setCenterHoverRevealSuppressed(true);
        });
    }

    function close() {
        // The panel stays mapped through its fade-out, so the editor's
        // focus-loss handler cannot be relied on here.
        flushEdit();
        setCenterHoverRevealSuppressed(false);
        root.controller.hide();
    }

    function toggle() {
        if (root.opened)
            root.close();
        else
            root.openFromHotkey();
    }

    function closeForPopoutSwitch() {
        root.close();
    }

    function openQuickAdd() {
        // GlobalShortcut is instantiated once per monitor. Only the focused
        // monitor's widget should open the overlay.
        var focusedMonitor = Hyprland.focusedMonitor;
        var screen = root.hostWindow ? root.hostWindow.screen : null;
        if (!screen || !focusedMonitor || screen.name !== focusedMonitor.name)
            return;

        // Keep the quick-add overlay as the only active panel, so one Escape
        // always closes the menu instead of revealing the todo panel beneath it.
        if (root.opened)
            root.close();
        root.quickAddOpen = true;
        Qt.callLater(function () {
            if (root.quickAddOpen) {
                quickAddField.forceActiveFocus();
                quickAddField.selectAll();
            }
        });
    }

    function closeQuickAdd() {
        root.quickAddOpen = false;
        quickAddField.clear();
    }

    function switchPanel(direction) {
        if (root.bar && typeof root.bar.switchPanelFrom === "function")
            return root.bar.switchPanelFrom(root.barIdentity, direction);
        return false;
    }

    function setCenterHoverRevealSuppressed(value) {
        if (root.bar && typeof root.bar.setCenterHoverRevealSuppressed === "function")
            root.bar.setCenterHoverRevealSuppressed(value);
    }

    function recount() {
        var n = 0;
        for (var i = 0; i < todoModel.count; ++i)
            if (!todoModel.get(i).completed)
                n++;
        remaining = n;
    }

    function normalizeSelection() {
        if (todoModel.count === 0)
            selectedIndex = -1;
        else if (selectedIndex < 0)
            selectedIndex = 0;
        else if (selectedIndex >= todoModel.count)
            selectedIndex = todoModel.count - 1;
    }

    function selectTodo(index, focusList) {
        if (index < 0 || index >= todoModel.count)
            return;
        selectedIndex = index;
        if (focusList)
            keyCatcher.forceActiveFocus();
    }

    function moveSelection(delta) {
        if (todoModel.count === 0) {
            selectedIndex = -1;
            return;
        }
        if (selectedIndex < 0)
            selectedIndex = delta > 0 ? 0 : todoModel.count - 1;
        else
            selectedIndex = Math.max(0, Math.min(todoModel.count - 1, selectedIndex + delta));
    }

    // The ListView is as tall as its whole model and the surrounding Flickable
    // scrolls the panel, so reveal the selected delegate through that Flickable.
    function ensureSelectedVisible() {
        if (selectedIndex < 0)
            return;
        Qt.callLater(function () {
            var row = todoList.itemAtIndex(root.selectedIndex);
            if (!row)
                return;
            var rowTop = row.mapToItem(todoScroll.contentItem, 0, 0).y;
            var rowBottom = rowTop + row.height;
            if (rowTop < todoScroll.contentY)
                todoScroll.contentY = rowTop;
            else if (rowBottom > todoScroll.contentY + todoScroll.height)
                todoScroll.contentY = rowBottom - todoScroll.height;
        });
    }

    function loadTodos(raw) {
        var todos = Model.parseTodos(raw);
        var previousSelectedIndex = selectedIndex;
        selectedIndex = -1;
        todoModel.clear();
        for (var i = 0; i < todos.length; ++i)
            todoModel.append(todos[i]);
        if (todoModel.count === 0)
            selectedIndex = -1;
        else if (previousSelectedIndex < 0)
            selectedIndex = 0;
        else
            selectedIndex = Math.min(previousSelectedIndex, todoModel.count - 1);
        recount();
    }

    function saveTodos() {
        var todos = [];
        for (var i = 0; i < todoModel.count; ++i) {
            var item = todoModel.get(i);
            todos.push({
                title: item.title,
                completed: item.completed
            });
        }
        todoFile.setText(JSON.stringify(todos, null, 2) + "\n");
    }

    function cancelEdit() {
        root.editingIndex = -1;
    }

    function flushEdit() {
        if (root.editingIndex >= 0 && root.activeEditor)
            root.commitEdit(root.editingIndex, root.activeEditor.text);
        root.editingIndex = -1;
    }

    // Right-click entry point. Any row already being edited is committed
    // first; otherwise its text is dropped when the delegate loses focus.
    function startEdit(index) {
        if (index < 0 || index >= todoModel.count)
            return;
        selectedIndex = index;
        if (root.editingIndex !== index)
            flushEdit();
        root.editingIndex = index;
    }

    // editingIndex is cleared before saveTodos(): the save round-trips
    // through FileView and rebuilds the model, destroying the editor.
    function commitEdit(index, text) {
        var title = String(text).replace(/^\s+|\s+$/g, "");
        root.editingIndex = -1;
        if (index < 0 || index >= todoModel.count)
            return;
        if (title === "" || title === todoModel.get(index).title)
            return;
        todoModel.setProperty(index, "title", title);
        saveTodos();
    }

    function addTodoTitle(text) {
        root.cancelEdit();
        var title = String(text).replace(/^\s+|\s+$/g, "");
        if (title === "")
            return false;
        var hadSelection = selectedIndex >= 0;
        todoModel.insert(0, {
            title: title,
            completed: false
        });
        if (hadSelection)
            selectedIndex++;
        normalizeSelection();
        saveTodos();
        recount();
        return true;
    }

    function addTodo() {
        if (!root.addTodoTitle(todoField.text))
            return;
        todoField.clear();
        todoField.forceActiveFocus();
    }

    function toggleTodo(index) {
        root.cancelEdit();
        if (index < 0 || index >= todoModel.count)
            return;
        var oldSelectedIndex = selectedIndex;
        var completed = !todoModel.get(index).completed;
        var destination = completed ? todoModel.count - 1 : 0;
        todoModel.setProperty(index, "completed", completed);
        // Keep the two states separated without changing order within either group.
        todoModel.move(index, destination, 1);
        if (oldSelectedIndex === index)
            selectedIndex = destination;
        else if (index < oldSelectedIndex && destination >= oldSelectedIndex)
            selectedIndex = oldSelectedIndex - 1;
        else if (index > oldSelectedIndex && destination <= oldSelectedIndex)
            selectedIndex = oldSelectedIndex + 1;
        normalizeSelection();
        saveTodos();
        recount();
    }

    function removeTodo(index) {
        root.cancelEdit();
        if (index < 0 || index >= todoModel.count)
            return;
        if (index < selectedIndex)
            selectedIndex--;
        else if (index === selectedIndex && index === todoModel.count - 1)
            selectedIndex = todoModel.count > 1 ? index - 1 : -1;
        todoModel.remove(index);
        normalizeSelection();
        saveTodos();
        recount();
    }

    function clearCompleted() {
        root.cancelEdit();
        var removed = false;
        for (var i = todoModel.count - 1; i >= 0; --i) {
            if (todoModel.get(i).completed) {
                if (i < selectedIndex)
                    selectedIndex--;
                else if (i === selectedIndex) {
                    if (i < todoModel.count - 1)
                        selectedIndex = i;
                    else if (todoModel.count > 1)
                        selectedIndex = i - 1;
                    else
                        selectedIndex = -1;
                }
                todoModel.remove(i);
                removed = true;
            }
        }
        if (removed) {
            normalizeSelection();
            saveTodos();
            recount();
        }
    }

    FileView {
        id: todoFile
        path: root.todoPath
        watchChanges: true
        atomicWrites: true
        printErrors: false
        onLoaded: root.loadTodos(text())
        onLoadFailed: root.loadTodos("[]")
        onFileChanged: reload()
    }

    Process {
        id: ensureDirsProc
        command: ["mkdir", "-p", root.stateDir]
        onExited: todoFile.reload()
    }

    Component.onCompleted: {
        ensureDirsProc.running = true;
    }

    GlobalShortcut {
        appid: "maduki-tech.omado"
        name: "quick-add"
        onPressed: root.openQuickAdd()
    }

    KeyboardPanel {
        id: panel
        anchorItem: root.anchorItem
        owner: root.barIdentity
        bar: root.bar
        open: root.opened
        centerOnBar: false
        focusTarget: todoField
        contentWidth: panel.fittedContentWidth(Style.space(440))
        contentHeight: panel.fittedContentHeight(todoColumn.implicitHeight)

        PanelKeyCatcher {
            id: keyCatcher
            anchors.fill: parent
            blocked: todoField.activeFocus || root.editingIndex >= 0
            onCloseRequested: root.close()
            onTabRequested: function (direction) {
                if (direction < 0)
                    todoField.forceActiveFocus();
                else
                    root.switchPanel(1);
            }
            onMoveRequested: function (dx, dy) {
                if (dy !== 0)
                    root.moveSelection(dy);
            }
            onActivateRequested: root.toggleTodo(root.selectedIndex)
            onDeleteRequested: {
                if (root.selectedIndex >= 0)
                    root.removeTodo(root.selectedIndex);
            }
            onTextKey: function (text) {
                if (text === "e")
                    root.startEdit(root.selectedIndex);
            }

            Flickable {
                id: todoScroll
                anchors.fill: parent
                contentWidth: width
                contentHeight: todoColumn.implicitHeight
                clip: true
                boundsBehavior: Flickable.StopAtBounds
                interactive: contentHeight > height

                Column {
                    id: todoColumn
                    width: todoScroll.width
                    spacing: Style.space(12)

                    Row {
                        width: parent.width
                        leftPadding: Style.space(16)
                        rightPadding: Style.space(16)
                        spacing: Style.space(10)

                        Text {
                            text: root.label
                            color: root.bar.foreground
                            font.family: root.bar.fontFamily
                            font.pixelSize: Style.font.display
                            anchors.verticalCenter: parent.verticalCenter
                        }

                        Column {
                            spacing: Style.space(2)
                            anchors.verticalCenter: parent.verticalCenter

                            Text {
                                text: "TODO LIST"
                                color: root.bar.foreground
                                font.family: root.bar.fontFamily
                                font.pixelSize: Style.font.title
                                font.bold: true
                            }
                            Text {
                                text: root.remaining + " remaining"
                                color: Qt.darker(root.bar.foreground, 1.5)
                                font.family: root.bar.fontFamily
                                font.pixelSize: Style.font.bodySmall
                            }
                        }
                    }

                    Row {
                        width: parent.width
                        leftPadding: Style.space(16)
                        rightPadding: Style.space(16)
                        spacing: Style.space(8)

                        TextField {
                            id: todoField
                            width: parent.width - Style.space(80)
                            placeholderText: "Add a task…"
                            foreground: root.bar.foreground
                            font.family: root.bar.fontFamily

                            Keys.onPressed: function (event) {
                                if (event.key === Qt.Key_Return || event.key === Qt.Key_Enter) {
                                    root.addTodo();
                                    event.accepted = true;
                                } else if (event.key === Qt.Key_Escape) {
                                    root.close();
                                    event.accepted = true;
                                } else if (event.key === Qt.Key_Tab || event.key === Qt.Key_Backtab) {
                                    var backwards = (event.modifiers & Qt.ShiftModifier) || event.key === Qt.Key_Backtab;
                                    if (!backwards && todoModel.count > 0) {
                                        root.normalizeSelection();
                                        keyCatcher.forceActiveFocus();
                                    } else {
                                        root.switchPanel(backwards ? -1 : 1);
                                    }
                                    event.accepted = true;
                                }
                            }
                        }

                        Rectangle {
                            width: Style.space(40)
                            height: todoField.height
                            radius: Style.cornerRadius
                            color: addArea.containsMouse ? Style.hoverFillFor(root.bar.foreground, Color.accent) : "transparent"

                            Text {
                                anchors.centerIn: parent
                                text: ""
                                color: root.bar.foreground
                                font.family: root.bar.fontFamily
                                font.pixelSize: Style.font.title
                            }

                            MouseArea {
                                id: addArea
                                anchors.fill: parent
                                hoverEnabled: true
                                cursorShape: Qt.PointingHandCursor
                                onClicked: root.addTodo()
                            }
                        }
                    }

                    Rectangle {
                        width: parent.width - Style.space(32)
                        x: Style.space(16)
                        height: Style.spacing.hairline
                        color: root.bar.foreground
                        opacity: 0.12
                    }

                    ListView {
                        id: todoList
                        width: parent.width
                        height: Math.max(Style.space(48), contentHeight)
                        interactive: false
                        model: todoModel
                        spacing: Style.space(4)
                        currentIndex: root.selectedIndex
                        onCurrentIndexChanged: root.ensureSelectedVisible()

                        delegate: Rectangle {
                            id: todoRow
                            required property int index
                            required property string title
                            required property bool completed

                            readonly property bool editing: root.editingIndex === index
                            readonly property bool selected: root.selectedIndex === index

                            width: todoList.width - Style.space(32)
                            x: Style.space(16)
                            height: Style.space(46)
                            radius: Style.cornerRadius
                            color: todoRow.selected
                                ? Style.selectedFillFor(root.bar.foreground, Color.accent)
                                : rowArea.containsMouse ? Style.hoverFillFor(root.bar.foreground, Color.accent) : "transparent"

                            Row {
                                anchors.fill: parent
                                anchors.leftMargin: Style.space(12)
                                anchors.rightMargin: Style.space(8)
                                spacing: Style.space(10)

                                Text {
                                    text: completed ? "󰄲" : "󰄱"
                                    color: completed ? Color.accent : root.bar.foreground
                                    font.family: root.bar.fontFamily
                                    font.pixelSize: Style.font.title
                                    anchors.verticalCenter: parent.verticalCenter
                                }

                                Text {
                                    visible: !todoRow.editing
                                    id: titleText
                                    width: parent.width - Style.space(72)
                                    text: title
                                    color: completed ? Qt.darker(root.bar.foreground, 1.6) : root.bar.foreground
                                    font.family: root.bar.fontFamily
                                    font.pixelSize: Style.font.body
                                    font.strikeout: completed
                                    elide: Text.ElideRight
                                    anchors.verticalCenter: parent.verticalCenter

                                    PanelToolTip {
                                        visible: rowArea.containsMouse && titleText.truncated
                                        text: title
                                        fontFamily: root.bar.fontFamily
                                    }
                                }

                                TextField {
                                    id: editField
                                    visible: todoRow.editing
                                    width: parent.width - Style.space(72)
                                    foreground: root.bar.foreground
                                    font.family: root.bar.fontFamily
                                    verticalPadding: Style.space(2)
                                    anchors.verticalCenter: parent.verticalCenter

                                    function beginEdit() {
                                        root.activeEditor = editField;
                                        text = todoRow.title;
                                        forceActiveFocus();
                                        selectAll();
                                    }

                                    // A save rebuilds the model, so a delegate can be
                                    // created with editing already true, in which case
                                    // onVisibleChanged never fires.
                                    Component.onCompleted: if (visible)
                                        Qt.callLater(beginEdit)
                                    onVisibleChanged: if (visible)
                                        beginEdit()

                                    // Clicking away keeps the edit rather than dropping
                                    // it. commitEdit() clears editingIndex first, so
                                    // this cannot recurse.
                                    onActiveFocusChanged: {
                                        if (!activeFocus && todoRow.editing)
                                            root.commitEdit(todoRow.index, editField.text);
                                    }

                                    Keys.onPressed: function (event) {
                                        if (event.key === Qt.Key_Return || event.key === Qt.Key_Enter) {
                                            root.commitEdit(todoRow.index, editField.text);
                                            Qt.callLater(function () { keyCatcher.forceActiveFocus(); });
                                            event.accepted = true;
                                        } else if (event.key === Qt.Key_Escape) {
                                            root.cancelEdit();
                                            Qt.callLater(function () { keyCatcher.forceActiveFocus(); });
                                            event.accepted = true;
                                        } else if (event.key === Qt.Key_Tab || event.key === Qt.Key_Backtab) {
                                            event.accepted = true;
                                        }
                                    }
                                }

                            }

                            Rectangle {
                                id: trashButton
                                width: Style.space(32)
                                height: Style.space(32)
                                x: parent.width - width - Style.space(8)
                                y: (parent.height - height) / 2
                                z: 10
                                radius: Style.cornerRadius
                                color: trashArea.containsMouse ? Style.hoverFillFor(root.bar.foreground, Color.accent) : "transparent"

                                Text {
                                    anchors.centerIn: parent
                                    text: "󰆴"
                                    color: trashArea.containsMouse ? Color.accent : Qt.darker(root.bar.foreground, 1.4)
                                    font.family: root.bar.fontFamily
                                    font.pixelSize: Style.font.body
                                }

                                MouseArea {
                                    id: trashArea
                                    anchors.fill: parent
                                    hoverEnabled: true
                                    cursorShape: Qt.PointingHandCursor
                                    onClicked: {
                                        root.selectTodo(index, true);
                                        root.removeTodo(index);
                                    }
                                }
                            }

                            MouseArea {
                                id: rowArea
                                anchors.fill: parent
                                anchors.rightMargin: Style.space(48)
                                z: -1
                                hoverEnabled: true
                                acceptedButtons: Qt.LeftButton | Qt.RightButton
                                cursorShape: Qt.PointingHandCursor
                                onClicked: function (mouse) {
                                    if (mouse.button === Qt.RightButton) {
                                        root.selectTodo(index, false);
                                        root.startEdit(index);
                                    } else {
                                        root.selectTodo(index, true);
                                        root.toggleTodo(index);
                                    }
                                }
                            }
                        }
                    }

                    Text {
                        visible: todoModel.count === 0
                        width: parent.width
                        horizontalAlignment: Text.AlignHCenter
                        text: "No tasks yet"
                        color: Qt.darker(root.bar.foreground, 1.5)
                        font.family: root.bar.fontFamily
                        font.pixelSize: Style.font.body
                        font.italic: true
                    }

                    Row {
                        visible: remaining < todoModel.count
                        width: parent.width
                        leftPadding: Style.space(16)
                        rightPadding: Style.space(16)

                        Text {
                            text: "Clear completed"
                            color: clearArea.containsMouse ? Color.accent : Qt.darker(root.bar.foreground, 1.4)
                            font.family: root.bar.fontFamily
                            font.pixelSize: Style.font.bodySmall

                            MouseArea {
                                id: clearArea
                                anchors.fill: parent
                                anchors.margins: -Style.space(6)
                                hoverEnabled: true
                                cursorShape: Qt.PointingHandCursor
                                onClicked: root.clearCompleted()
                            }
                        }
                    }
                }
            }
        }
    }

    PanelWindow {
        id: quickAddWindow
        screen: root.hostScreen
        visible: root.quickAddOpen && !!root.hostScreen
        color: "transparent"
        exclusionMode: ExclusionMode.Ignore
        WlrLayershell.layer: WlrLayer.Overlay
        WlrLayershell.keyboardFocus: root.quickAddOpen && !!root.hostScreen
            ? WlrKeyboardFocus.Exclusive : WlrKeyboardFocus.None
        WlrLayershell.namespace: "maduki-tech-omado-quick-add"
        anchors {
            top: true
            bottom: true
            left: true
            right: true
        }

        Rectangle {
            anchors.fill: parent
            color: Qt.rgba(0, 0, 0, 0.35)

            MouseArea {
                anchors.fill: parent
                onClicked: root.closeQuickAdd()
            }

            Rectangle {
                id: quickAddCard
                anchors.centerIn: parent
                width: Math.min(parent.width - Style.space(48), Style.space(520))
                height: quickAddColumn.implicitHeight + Style.space(40)
                radius: Style.cornerRadius * 2
                color: Color.popups.background
                border.color: Color.accent
                border.width: Style.normalBorderWidth

                Column {
                    id: quickAddColumn
                    anchors {
                        left: parent.left
                        right: parent.right
                        verticalCenter: parent.verticalCenter
                        margins: Style.space(20)
                    }
                    spacing: Style.space(10)

                    Text {
                        text: "QUICK ADD"
                        color: root.bar.foreground
                        font.family: root.bar.fontFamily
                        font.pixelSize: Style.font.title
                        font.bold: true
                    }

                    TextField {
                        id: quickAddField
                        width: parent.width
                        placeholderText: "Add a task…"
                        foreground: root.bar.foreground
                        font.family: root.bar.fontFamily

                        Keys.onPressed: function (event) {
                            if (event.key === Qt.Key_Return || event.key === Qt.Key_Enter) {
                                if (root.addTodoTitle(text))
                                    root.closeQuickAdd();
                                event.accepted = true;
                            } else if (event.key === Qt.Key_Escape) {
                                root.closeQuickAdd();
                                event.accepted = true;
                            }
                        }
                    }

                    Text {
                        text: "Enter to add · Esc to close"
                        color: Qt.darker(root.bar.foreground, 1.5)
                        font.family: root.bar.fontFamily
                        font.pixelSize: Style.font.bodySmall
                    }
                }
            }
        }
    }
}
